buildscript {
    repositories {
        mavenCentral()
        maven {
            name = "Forge"
            url = "https://maven.minecraftforge.net/"
        }
    }
    dependencies {
        classpath group: "net.minecraftforge.gradle", name: "ForgeGradle", version: "6.+", changing: true
    }
}

apply plugin: "net.minecraftforge.gradle"
apply plugin: "eclipse"
apply plugin: "maven-publish"

version = "${mod_version}"
group = "${mod_group}.${mod_id}"
archivesBaseName = "${mod_id}-${minecraft_version}"

java.toolchain.languageVersion = JavaLanguageVersion.of(8)
java.withSourcesJar()
java.withJavadocJar()

minecraft {
    mappings channel: "official", version: "${mappings_version}"
    runs {
        client {
            workingDirectory project.file("run")
            property "forge.logging.markers", "REGISTRIES"
            property "forge.logging.console.level", "debug"
            mods {
                expandedgamerules {
                    source sourceSets.main
                }
            }
        }

        server {
            workingDirectory project.file("run")
            property "forge.logging.markers", "REGISTRIES"
            property "forge.logging.console.level", "debug"
            mods {
                expandedgamerules {
                    source sourceSets.main
                }
            }
        }

        data {
            workingDirectory project.file("run")
            property "forge.logging.markers", "REGISTRIES"
            property "forge.logging.console.level", "debug"
            args "--mod", "${mod_id}", "--all", "--output", file("src/generated/resources/"), "--existing", file("src/main/resources/")
            mods {
                expandedgamerules {
                    source sourceSets.main
                }
            }
        }
    }
}

sourceSets.main.resources { srcDir "src/generated/resources" }

tasks.withType(JavaCompile).configureEach {
    it.options.encoding = 'UTF-8'
}

repositories {
    maven {
        name = "Create"
        url = "https://maven.tterrag.com/"
    }
    maven {
        name = "Curios"
        url = "https://maven.theillusivec4.top/"
    }
    exclusiveContent {
        forRepository {
            maven {
                url "https://cursemaven.com"
            }
        }
        filter {
            includeGroup "curse.maven"
        }
    }
    flatDir {
        dirs "localMods"
    }
}

dependencies {
    minecraft "net.minecraftforge:forge:${minecraft_version}-${forge_version}"
    implementation fg.deobf("create:create:${create_version}")
    runtimeOnly fg.deobf("top.theillusivec4.curios:curios-forge:${curios_version}")
    compileOnly fg.deobf("top.theillusivec4.curios:curios-forge:${curios_version}:api")
}

processResources {
    def expandProps = [
            "mod_version": mod_version,
            "mod_group": mod_group,
            "minecraft_version": minecraft_version,
            "forge_version": forge_version,
            "forge_loader_version_range": forge_loader_version_range,
            "forge_version_range": forge_version_range,
            "minecraft_version_range": minecraft_version_range,
            "curios_version_range": curios_version_range,
            "create_version_range": create_version_range,
            "mod_id": mod_id,
            "mod_name": mod_name,
            "mod_description": mod_description,
            "mod_author": mod_author,
            "mod_license": mod_license,
            "mod_credits": mod_credits
    ]

    filesMatching(['pack.mcmeta', 'META-INF/mods.toml']) {
        expand expandProps
    }
    inputs.properties(expandProps)
}

jar {
    from(rootProject.file("LICENSE")) {
        rename { "LICENSE_${mod_name}.md" }
    }
    manifest {
        attributes([
            "FMCorePluginContainsFMLMod": "true",
            "Specification-Title": "${mod_name}",
            "Specification-Vendor": "${mod_author}",
            "Specification-Version": "1",
            "Implementation-Title": "${mod_name}",
            "Implementation-Version": "${mod_version}",
            "Implementation-Vendor" : "${mod_author}",
            "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}

sourcesJar {
    from(rootProject.file("LICENSE.md")) {
        rename { "LICENSE_${mod_name}.md" }
    }
}

jar.finalizedBy("reobfJar")

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact jar
        }
    }
    repositories {
        maven {
            url "file:///${project.projectDir}/mcmodsrepo"
        }
    }
}

tasks.withType(GenerateModuleMetadata).configureEach {
    enabled = false
}
